#!/bin/sh
#|-*- mode:lisp -*-|#
#|Install a given implementation or a system for roswell environment
exec ros -Q +R -L sbcl-bin -- $0 "$@"
|#

(progn
  (ros:include "util")
  (ros:include "util-install")
  (when (probe-file (merge-pathnames "setup.lisp" (ros:opt "quicklisp")))
    (ros:include "util-install-quicklisp")))

(defpackage :ros.script.install.3671936092
  (:use :cl :ros.util :ros.locations :ros.install))

(in-package :ros.script.install.3671936092)

(defun main (subcmd impl/version &rest argv)
  (let* (imp
         (pos (position #\/ impl/version))
         (*ros-path* (make-pathname :defaults (ros:opt "argv0")))
         version)
    (if pos
        (setq version (subseq impl/version (1+ pos))
              imp (subseq impl/version 0 pos))
        (setq imp impl/version))
    (read-call "quicklisp-client:register-local-projects")
    (cond ((install-impl-if-probed imp version subcmd argv))
          ((install-script-if-probed impl/version))
          ((install-system-if-probed imp))
          (version
           ;; tbd: think how to set version for impl,branch/tag/hash for git.
           (format *error-output* "install from github ~A/~A~%" imp version)
           (clone-github imp version :path "local-projects")
           (read-call "quicklisp-client:register-local-projects")
           (or (install-impl-if-probed version nil subcmd argv)
               (install-system-if-probed version)))
          (t (format *error-output* "'~A' is not a valid target for 'install' -- It should be a name of either:
+ a quicklisp-installable system
+ a common lisp installation ~%" imp)
             (ros:quit 1)))
    (ros:exec `(,(ros:opt "argv0") "setup"))))
;;; vim: set ft=lisp lisp:
