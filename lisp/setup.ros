#!/bin/sh
#|-*- mode:lisp -*-|#
#|Initial setup
exec ros -Q -L sbcl-bin -- $0 "$@"
|#

(progn
  (roswell:include "util-dump"))

(defpackage :ros.script.setup.3672012282
  (:use :cl :roswell.util :roswell.util.dump))
(in-package :ros.script.setup.3672012282)

(defun split-spaces (line)
  (loop with from = 0
        with length = (length line)
        for to from 0
        when (or (eql length to)
                 (eql (aref line to) #\space))
        collect (prog1
                    (subseq line from to)
                  (setq from (1+ to)))
        until (eql length to)))

(defun dump-filter (name)
  (lambda (file)
    (ignore-errors
     (with-open-file (in file)
       (let (line pos package)
         (and (equal (read-line in) "#!/bin/sh")
              (equal "#|" (subseq (setq line (read-line in)) 0 2))
              (equal "|#" (subseq line (- (length line) 2) (length line)))
              (equal "#|" (subseq (setq line (read-line in)) 0 2))
              (setq line (split-spaces (read-line in)))
              (setq pos (position "-m" line :test 'equal))
              (equal (nth (1+ pos) line) name)
              (equal "|#" (subseq (read-line in) 0 2))
              (eql 'progn (first (read in)))
              (eql 'defpackage (first (setq line (read in))))
              (setq package (second line))
              (eql 'in-package (first (setq line (read in))))
              (eql package (second line))
              (> (parse-integer (subseq (string package) (1+ (position #\. (string package) :from-end t))))
                 3500000000)
              (setq in (make-string-input-stream
                        (with-output-to-string (o)
                          (loop for l = (read-line in nil nil)
                                while l
                                do (format o "~A~%" (substitute #\_ #\: l))))))
              (loop with end = (gensym)
                    for i = (read in nil end)
                    until (eql i end)
                    always (member (first i) '(defun defvar defparameter quote)))))))))

(defun main (&optional (dump "roswell") &rest argv)
  (declare (ignore argv))
  #-win32
  (ignore-errors
    (require :sb-posix)
    (sb-posix:setgid (parse-integer (sb-posix:getenv "SUDO_GID")))
    (sb-posix:setuid (parse-integer (sb-posix:getenv "SUDO_UID"))))
  (unless (probe-file (merge-pathnames "setup.lisp" (opt "quicklisp")))
    (format *error-output* "Installing Quicklisp...~%")
    (flet ((opt (x)
             (when (opt x)
               (list (format nil "~A=~A" x (opt x))))))
      (let ((params `(,@(opt "quicklisp.client")
                        ,@(opt "quicklisp.dist")
                        "install"
                        "quicklisp")))
        (when (roswell:verbose)
          (format *error-output* "Parameter for install quicklisp '~{~A ~}'~%" params))
        (roswell:roswell params :interactive nil))))
  (let* ((dir (sort
               (delete-if-not
                (dump-filter dump)
                (append (directory (make-pathname :defaults *load-pathname* :name :wild :type "ros"))
                        (directory (merge-pathnames "cmds/*.ros" (homedir)))
                        (directory (merge-pathnames "lisp/*.lisp" (homedir)))
                        (directory (merge-pathnames "bin/*" (homedir))))) #'string< :key 'pathname-name))
         (dir (append (remove "init.ros" dir :key #'file-namestring :test (complement #'equal))
                      (remove "init.ros" dir :key #'file-namestring :test 'equal)))
         (use-env (equal dump "roswell")))
    (when (roswell:verbose)
      (format *error-output* "Targets for dump:~%~{~A~%~}~%" dir))
    (when (or (not (equal dump "roswell"))
              (roswell:verbose))
      (format *error-output* "building dump:~A~%" (make-pathname :defaults (dump-dir use-env) :name dump :type "core")))
    (force-output *error-output*)
    (unless (ignore-errors
              (when (> (file-write-date
                        (make-pathname :defaults (dump-dir use-env) :name dump :type "core"))
                       (loop for i in dir
                          maximize (file-write-date i)))
                (format *error-output* "up to date. stop~%")
                t))
      (roswell:roswell `(,(if (roswell:verbose) "-v" "") " +R" " +Q" " -L " "sbcl-bin"
                          " --eval " #-win32 "'" "(roswell:ignore-shebang)" #-win32 "'" " "
                          ,@(loop for i in dir
                               collect "--load "
                                    collect (format nil "~A " i))
                          "dump output -f " ,dump)
                       t nil))))
;;; vim: set ft=lisp lisp:
