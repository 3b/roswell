#!/bin/sh
#|-*- mode:lisp -*-|#
#|Delete installed implementations
exec ros +Q -L sbcl-bin -- $0 "$@"
|#

(defpackage :ros.delete
  (:use :cl))
(in-package :ros.delete)

(ros:quicklisp :environment nil)

;; sbcl-bin is old enough not to include UIOP on some OSes like Mac OS X.
(unless (find-package :uiop)
  (ql:quickload :uiop :silent t))

(defun homedir ()
  (ros:opt "homedir"))

(defun roswell-internal (args output)
  (uiop/run-program:run-program
   (format nil "~A roswell-internal-use~{ ~A~}"
           (ros:opt "argv0") args) :output output))

(defun uname ()
  (remove #\Newline (remove #\Return (roswell-internal `("uname") :string))))

(defun uname-m ()
  (remove #\Newline (remove #\Return (roswell-internal `("uname" "-m") :string))))

(defun main (subcmd cmd &rest r)
  (cond ((and (equal subcmd "main") r)
         (let* ((impl-name (first r))
                (path (merge-pathnames (format nil "impls/~A/~A/~A/"
                                               (uname-m) (uname) impl-name)
                                       (homedir))))
           (unless (position :up (pathname-directory path))
             (if (probe-file path)
                 (progn
                   (uiop/filesystem:delete-directory-tree path :validate t)
                   (format t "~&~A has successfully deleted.~%" impl-name))
                 (format *error-output* "~&~A is not installed yet.~%" impl-name)))))
        ((or (equal subcmd "help") (not r))
         (format t "Usage: ~A [OPTIONS] ~A impl/version ~%"
                 (ros:opt "argv0") cmd))))
