#!/bin/sh
#|-*- mode:lisp -*-|#
#|Delete installed implementations
exec ros +Q -L sbcl-bin -- $0 "$@"
|#

(defpackage :ros.delete
  (:use :cl))
(in-package :ros.delete)

(ros:quicklisp :environment nil)

;; sbcl-bin is old enough not to include UIOP on some OSes like Mac OS X.
(unless (find-package :uiop)
  (ql:quickload :uiop :silent t))

(defun homedir ()
  (ros:opt "homedir"))

(defun uname ()
  (ros:roswell '("roswell-internal-use uname") :string t))

(defun uname-m ()
  (ros:roswell '("roswell-internal-use uname -m") :string t))

(defun config (c)
  (ros:roswell (list "config show" c) :string t))

(defun (setf config) (a b)
  (ros:roswell (list "config" b a) :string t)
  a)

(defun main (subcmd cmd &rest r)
  (let* ((default (config "default.lisp"))
         (verstring (format nil "~A.version" default))
         (config (format nil "~A/~A" default (config verstring))))
    (cond ((and (equal subcmd "main") r)
           (let* ((impl-name (first r))
                  (path (merge-pathnames (format nil "impls/~A/~A/~A/"
                                                 (uname-m) (uname) impl-name)
                                         (homedir))))
             (unless (position :up (pathname-directory path))
               (cond ((probe-file path)
                      (uiop/filesystem:delete-directory-tree path :validate t)
                      (format t "~&~A has successfully deleted.~%" impl-name)
                      (when (equal impl-name config)
                        (format t "~&clear config ~S~%" verstring)
                        (setf (config verstring) "")))
                     (t (format *error-output* "~&~A is not installed yet.~%" impl-name))))))
          ((or (equal subcmd "help") (not r))
           (format t "Usage: ~A [OPTIONS] ~A impl/version ~%"
                   (ros:opt "argv0") cmd)))))
